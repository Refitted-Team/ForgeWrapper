import java.text.SimpleDateFormat

plugins {
    id "java"
    id "eclipse"
    id "maven-publish"
}

sourceCompatibility = targetCompatibility = 21
compileJava {
    sourceCompatibility = targetCompatibility = 21
}

version = "${fw_version}"
group = "io.github.zekerzhayard"
archivesBaseName = rootProject.name

configurations {
    provided {
        implementation.extendsFrom provided
    }
    multirelase {
        implementation.extendsFrom multirelase
    }
}

repositories {
    mavenCentral() {
        name = "Maven Central"
        content {
            excludeGroup("net.neoforged")
        }
    }
    maven {
        name = "MinecraftForge"
        url = "https://maven.minecraftforge.net/"
    }
    maven {
        name = "NeoForged"
        url = "https://maven.neoforged.net/releases/"
    }
    maven {
        name = "Minecraft"
        url "https://libraries.minecraft.net"
    }
}

dependencies {
    compileOnly "com.google.code.gson:gson:2.8.7"
    compileOnly "cpw.mods:modlauncher:8.0.9"
    compileOnly "net.minecraftforge:installer:2.2.7"
    compileOnly "net.minecraftforge:forgespi:7.0.1"
    compileOnly "net.minecraftforge:fmlloader:1.21.1-52.0.4@jar"
    compileOnly "net.sf.jopt-simple:jopt-simple:5.0.4"

    compileOnly "net.neoforged.fancymodloader:loader:4.0.27"
    compileOnly "net.neoforged.fancymodloader:core:2.0.7"
    compileOnly "net.neoforged.fancymodloader:spi:3.0.9"

    provided project(":common")
    provided project(":legacy")
    multirelase project(":jigsaw")
}

java {
    withSourcesJar()
}

jar {
    manifest.attributes([
        "Specification-Title": "${project.name}",
        "Specification-Vendor": "ZekerZhayard",
        "Specification-Version": "${project.version}".split("-")[0],
        "Implementation-Title": "${project.name}",
        "Implementation-Version": "${project.version}",
        "Implementation-Vendor" :"ZekerZhayard",
        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
        "Automatic-Module-Name": "${project.group}.${project.archivesBaseName}".toString().toLowerCase(),
        "Multi-Release": "true",
        "GitCommit": String.valueOf(System.getenv("GITHUB_SHA"))
    ])

    from configurations.provided.files.collect {
        zipTree(it)
    }

    into "META-INF/versions/9", {
        from configurations.multirelase.files.collect {
            zipTree(it)
        }
        exclude "META-INF/**"
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId "${project.group}"
            artifactId "${project.archivesBaseName}"
            version "${project.version}"

            from components.java
        }
    }
    repositories {
        maven {
            url = layout.buildDirectory.dir("maven")
        }
    }
}
tasks.publish.dependsOn build

static String getVersionSuffix() {
    if (System.getenv("IS_PUBLICATION") != null || System.getenv("GITHUB_ACTIONS") == "true")
        return new SimpleDateFormat("-yyyy-MM-dd").format(new Date())

    return "-LOCAL"
}
